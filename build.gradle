plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

// apply dependency file for a simple version management strategy.
apply from: "$projectDir/gradle/dependencies.gradle"

group = 'net.dovahkiinforged'
// get the system env var for version during release tagging in GitHub
// or default to a static string.
version = System.getenv("VERSION") ?: '1.0.3-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // JUint
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    // Lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/DylanBulmer/dovahkiin-forged-commons"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
